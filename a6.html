<html>
  <head>
    <meta charset="UTF-8">
    <title>CPSC 314 Assignment 6 Jan 2018</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
    <script src="js/three.js"></script>
    <script src="js/OBJLoader.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/KeyboardState.js"></script>
    <!-- By Jerome Etienne: http://jetienne.com/ -->

<script id="raytracerVertShader" type="x-shader/x-vertex">
varying vec3 vcsPosition;
varying vec3 vcsNormal;
uniform mat4 matrixWorld;


void main()
{
  vec4 vcsP = modelViewMatrix * vec4(position,1.0);

  gl_Position = projectionMatrix * modelViewMatrix * vec4(position,1.0);
  vcsNormal = normalMatrix * normal;
  vcsPosition = vcsP.xyz;
}

</script>

<script id="raytracerFragShader" type="x-shader/x-fragment">
//VARYING
varying vec3 vcsPosition;
varying vec3 vcsNormal;

//UNIFORMS
uniform vec2 resolution;
uniform vec3 lightPosition;
uniform vec3 light_color;
uniform float myFloat1;
uniform float myFloat2;

const int n_spheres = 3;
float epsilon = 0.00001;
float largeT = 1000000.0;
vec3 backgroundColor = vec3(0.5,0.5,1.0);
mat4 planeMatrix = mat4(vec4(1,0,0,0),vec4(0,0,1,0),vec4(0,1,0,0),vec4(0,0,0,1));


struct light {
  vec3 color;
  vec3 position;
};

struct ray {
  vec3 origin;
  vec3 direction;
  int bounces;
};

struct material {
  float kLocal;
  float kSpec;
  vec3 color;
};

struct sphere {
  material mtrl;
  vec3 position;
  float radius;
};

struct plane {
  material mtrl;
  vec3 position;
  float width;
  float height;
};

sphere nearestSphere;
sphere scene_spheres[n_spheres];

/////////////////////////////////////////////////////////////////////////////////
// sphereIntersect():  returns smallest posistive t value for a sphere intersection;
//                     returns -1 if no intersection
/////////////////////////////////////////////////////////////////////////////////

float sphere_intersect(ray myRay, sphere sphr) {
  float a = dot(myRay.direction, myRay.direction);
  vec3 s0_r0 = myRay.origin - sphr.position;
  float b = 2.0 * dot(myRay.direction, s0_r0);
  float c = dot(s0_r0, s0_r0) - (sphr.radius * sphr.radius);
  float d = sqrt(b*b-4.0*a*c);               // compute the discriminant
  if (d < 0.0) {                 // no solution to the quadratic equation?
    return -1.0;                   // then flag as no intersection
  } else {
    float t1 = (-b - d)/(2.0*a);   // compute both values of t
    float t2 = (-b + d)/(2.0*a);
    float tmin = min(t1,t2);
    float tmax = max(t1,t2);
    if (tmax<=0.0+epsilon) return -1.0;
    if (tmin<=0.0+epsilon) return -1.0;
    return (tmin);              // return smallest positive value
  }
}

/////////////////////////////////////////////////////////////////////////////////
// rayT():  cast a ray, and computes t for closest intersection in the direction of +t
//          If there is no intersection, it returns  largeT
/////////////////////////////////////////////////////////////////////////////////

float rayT(ray myRay)
{
  float nearest_t = largeT;
  float curr_t;
  for (int i = 0; i < n_spheres; ++i) {
    curr_t = sphere_intersect(myRay, scene_spheres[i]);  // test against sphere
    if (curr_t == -1.0) continue;         // missed sphere?
    else if (curr_t < nearest_t) {        // closest sphere?
      nearest_t = curr_t;
      nearestSphere = scene_spheres[i];
    }
  }
  return nearest_t;
}

/////////////////////////////////////////////////////////////////////////////////
// localShade():  compute local color for a surface point
/////////////////////////////////////////////////////////////////////////////////

vec3 localShade(vec3 P, vec3 N, vec3 I, vec3 surfColor) {
  ray shadowRay;
  // Compute L a normalized vector pointing to the light source
  vec3 L = -I;
  // Diffuse N*L
  float i = max(0.0, dot(N, L));
  // Shadow ray from P intersection to the lightsource
  shadowRay.origin = P;
  shadowRay.direction = L;
  shadowRay.bounces =0;

  // t value of intersection if less than large there
  // is something between the light source and point in surface
  float nearest_t = rayT(shadowRay);
  if(nearest_t < largeT)
    surfColor= vec3(0.0, 0.0, 0.0);

  // compute diffuse term
  vec3 diffuse_term = (light_color*surfColor)*i;

  // Compute specular
  vec3 V = normalize(-P);
  float d = 2.0*i;
  vec3 R = d*N-L;
  float n = 100.0;
  float rv = max(0.0, pow (max(0.0, dot(R, V)), n));
  vec3 specular_term= (light_color*surfColor)*rv;

  return specular_term+diffuse_term;
}

/////////////////////////////////////////////////////////////////////////////////
// bgColor(ray):  cast a ray, and compute a color, recursively if needed
/////////////////////////////////////////////////////////////////////////////////

vec3 bgColor(ray myRay)
{
//  return backgroundColor;
  vec4 origin = planeMatrix*vec4(myRay.origin, 1.0);       // transform ray into the coord system of the plane
  vec4 direction = planeMatrix*vec4(myRay.direction,0.0);  //

  float zPlane = -10.0;               // in local coords, the plane occupies the xy-plane at z=-10
  float t = (zPlane - origin.z)/direction.z;    // intersect ray with plane, in local plane coords
  if (t<0.0) return vec3(0.0, 0.0, 0.0);            // ray intersects behind the eye, so is looking away from the plane
  vec3 P = origin.xyz + t*direction.xyz;        // compute intersection point
  float xf = floor(fract(0.1*P.x)+0.5);   // 0 or 1    computations to compute checkerboard pattern
  float yf = floor(fract(0.1*P.y)+0.5);   // 0 or 1
  float sum = xf + yf;

  //vec3 P = (myRay.origin)+(myRay.direction*nearest_t);
  vec3 N = vec3(0.0, 0.0, 1.0);
  //vec3 I = reflect(normalize(myRay.direction), N)*(1.0);
  vec3 I = normalize(lightPosition-P);
  vec3 K ;


  //vec3 localColor = localShade(P,N,I,nearestSphere.mtrl.color);
  if (sum==1.0)
    K=vec3(0.3,0.3,0.3);      // black square
  else
    K=vec3(1.0,1.0,1.0);            // white square

  vec3 L = normalize(lightPosition-P);

  // Shadow Ray on checkerboard
  ray shadowRay;
  shadowRay.origin= P;
  shadowRay.direction = L;
  float nearest_t = rayT(myRay);                  // find closest object
  if (nearest_t < largeT) {
    K = vec3(0.0,0.0,0.0);
  }


  return max(0.0, dot(N, L)) * K;
}

/////////////////////////////////////////////////////////////////////////////////
// rayCast():  cast a ray, and compute a color, recursively if needed
/////////////////////////////////////////////////////////////////////////////////

vec3 rayCast(ray myRay)
{
  vec3 colour=vec3(0.0, 0.0, 0.0);
  float bounces= 0.0;
  float kSpec_acc= 1.0;
  bool hit = false;
  ray reflectedRay;
  for(int i =0; i<=5;i++){

    float nearest_t = rayT(myRay);                  // find closest object
    float kSpec = nearestSphere.mtrl.kSpec;         // keep these lines for use in step (7)
    float kLocal = nearestSphere.mtrl.kLocal;       // keep these lines for use in step (7)
    if (nearest_t < largeT) {       // hit an object?
      hit = true;
      vec3 P = (myRay.origin)+myRay.direction*nearest_t;
      vec3 N = normalize(P-nearestSphere.position);
      vec3 I = normalize(P- lightPosition);
      vec3 K = nearestSphere.mtrl.color;

      vec3 localColor = localShade(P,N,I,K);     // local shading
      reflectedRay.origin =P;
      reflectedRay.direction =reflect(normalize(myRay.direction), N);

      colour+= kSpec_acc*kLocal*localColor;
      kSpec_acc *= kSpec;
      myRay= reflectedRay;
      // return localColor;
    }else{
      colour+= kSpec_acc*kLocal*bgColor(myRay);       // return checkeboard texture
      break;
    }

  }
  return (hit? colour:bgColor(myRay));
}

void main()
{

  // INIT SPHERES
  sphere sphere0;
  sphere0.mtrl.color = vec3(0.5, 1.0, 0.5);
  sphere0.mtrl.kSpec = 0.2;
  sphere0.mtrl.kLocal = 1.0;
  sphere0.radius = 2.0;
  sphere0.position = vec3(0,0,-7.5);
  sphere0.position.x = -1.0 + myFloat1;
  sphere0.position.y = -1.0 + myFloat2;

  sphere sphere1;
  sphere1.mtrl.color = vec3(1.0, 1.0, 1.0);
  sphere1.mtrl.kSpec = 0.4;
  sphere1.mtrl.kLocal = 0.6;
  sphere1.radius = 1.0;
  sphere1.position = vec3(3,1,-8);

  sphere sphere2;
  sphere2.mtrl.color = vec3(1.0, 0.0, 0.0);
  sphere2.mtrl.kSpec = 0.0;
  sphere2.mtrl.kLocal = 1.0;
  sphere2.radius = 1.0;
  sphere2.position = vec3(0,3,-8);

  scene_spheres[0] = sphere0;
  scene_spheres[1] = sphere1;
  scene_spheres[2] = sphere2;

  ray pixelRay;
    // compute normalized screen coordinates for pixel:  [-1,1] in y;  [-a,a] in x, where a=aspect ratio
  vec2 uv = (-1.0 + 2.0*gl_FragCoord.xy / resolution.xy) * vec2(resolution.x/resolution.y, 1.0);
  pixelRay.origin = vec3(0,0,0);                        // ray starts at eye:  origin
  pixelRay.direction = normalize(vec3(0.5*uv,-1.0));    // look in the direction of a given pixel


  vec3 rayColor = rayCast(pixelRay);            // cast the initial ray
  gl_FragColor = vec4(rayColor, 1.0);           // assign color to fragment
}
</script>

<script src="a6.js"></script>
</body>
</html>
